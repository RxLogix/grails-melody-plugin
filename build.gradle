buildscript {
    ext {
        grailsVersion = project.grailsVersion

    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
    id "com.jfrog.bintray" version "1.2"
}

def removeSnapshot = { String s -> s.replace('-SNAPSHOT', '') }
def changeVersion = { String version, int to ->
    def versionParts = removeSnapshot(version).split('\\.')
    def versionPartChanged = Integer.parseInt(versionParts[1]) + to
    return "${versionParts[0]}.${versionPartChanged}.${versionParts[2]}"
}

def getJavaMelodyVersion = { String versionDependency ->
    def url = "http://central.maven.org/maven2/net/bull/javamelody/javamelody-core/${versionDependency}/javamelody-core-${versionDependency}.jar"
    def code = new URL(url).openConnection().with {
        requestMethod = 'HEAD'
        connect()
        responseCode
    }
    if (code == 404) {
        def newVersion = changeVersion(versionDependency, -1)
        println "Using version ${newVersion} of JavaMelody dependency"
        return newVersion
    }
    return versionDependency
}


def versionTxt = file("${rootProject.projectDir}/version.txt")
def versionPlugin = versionTxt.exists() ? versionTxt.text.trim() : '0.1-SNAPSHOT'
def versionDependency = getJavaMelodyVersion(removeSnapshot(versionPlugin))
group "org.grails.plugins"
version = versionPlugin


apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.grails-plugin-publish"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.grails:grails-plugin-testing"

    console "org.grails:grails-console"

    //plugin dependencies
    compile "net.bull.javamelody:javamelody-core:${versionDependency}"
    compile ("com.lowagie:itext:2.1.7") {
        exclude module: "bcmail-jdk14"
        exclude module: "bcprov-jdk14"
        exclude module: "bctsp-jdk14"
    }
    compile "org.jrobin:jrobin:1.5.9"

}

grailsPublish {
    githubSlug = 'javamelody/grails-melody-plugin'
    license {
        name = 'Apache-2.0'
    }
    title = "JavaMelody Grails Plugin"
    desc = "Integrate JavaMelody monitoring into Grails application."
    developers = [
            liuchao: 'Liu Chao',
            emericvernat: 'Emeric Vernat',
            sergiomichels: 'SÃ©rgio Michels'
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
